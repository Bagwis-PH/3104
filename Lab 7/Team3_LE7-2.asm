
DATA    SEGMENT
    ORG 03000H

P6_COM_REG   EQU 0F6H
P6_PROGRAM   EQU 081H

ONE_DISPLAY  EQU 0F0H
TWO_DISPLAY  EQU 0F2H
KYPAD_INPUT  EQU 0F4H
LED_OUTPUT   EQU 0F4H
DEFAULT_SEG  EQU 002H
BLINK_DELAY  EQU 07000H

PIC0         EQU 0F8H
PIC1         EQU 0FAH
ICW1         EQU 013H
ICW2         EQU 080H
ICW4         EQU 003H
OCW1         EQU 0FCH

VADD0        EQU 0200H
VADD1        EQU 0204H

DISP_SEGS DB 11111100B, 01100000B, 11011010B
          DB 11110010B, 01100110B, 10110110B
          DB 10111110B, 11100000B, 11111110B
          DB 11110110B, 00000010B, 00000001B
          DB 00000001B, 00000001B, 00000001B
          DB 00000001B

SEG_STATE DB 0AH

KPD_TABLE DB 01H, 02H, 03H, 0AH, 04H, 05H, 06H, 0AH
          DB 07H, 08H, 09H, 0AH, 0AH, 00H, 0AH, 0AH

DATA    ENDS


STACK   SEGMENT PARA STACK
    DW 64 DUP(?)
TOS DW ?
STACK   ENDS


HANDLER0 SEGMENT
ISR0 PROC FAR
    ASSUME CS:HANDLER0, DS:DATA, SS:STACK
    ORG 01000H

    PUSHF
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI

    MOV DX, LED_OUTPUT
    XOR AL, AL
    OUT DX, AL

    MOV DX, KYPAD_INPUT
    IN  AL, DX

    XOR BX, BX
    MOV BL, AL

    MOV SI, OFFSET KPD_TABLE
    MOV BL, [SI+BX]

    MOV SEG_STATE, BL

    MOV SI, OFFSET DISP_SEGS
    MOV AL, [SI+BX]

    MOV DX, ONE_DISPLAY
    OUT DX, AL

    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    POPF
    IRET
ISR0 ENDP
HANDLER0 ENDS


HANDLER1 SEGMENT
ISR1 PROC FAR
    ASSUME CS:HANDLER1, DS:DATA, SS:STACK
    ORG 02000H

    PUSHF
    PUSH AX
    PUSH BX
    PUSH DX
    PUSH SI

    XOR BX, BX
    MOV BL, SEG_STATE

    MOV SI, OFFSET DISP_SEGS
    MOV AL, [SI+BX]

    MOV DX, TWO_DISPLAY
    OUT DX, AL

    POP SI
    POP DX
    POP BX
    POP AX
    POPF
    IRET
ISR1 ENDP
HANDLER1 ENDS


CODE    SEGMENT PUBLIC 'CODE'
    ASSUME CS:CODE, DS:DATA, SS:STACK
    ORG 08000H

START:
    CLI

    MOV AX, DATA
    MOV DS, AX
    MOV AX, STACK
    MOV SS, AX
    LEA SP, TOS

    MOV DX, P6_COM_REG
    MOV AL, P6_PROGRAM
    OUT DX, AL

    MOV DX, ONE_DISPLAY
    MOV AL, DEFAULT_SEG
    OUT DX, AL

    MOV DX, TWO_DISPLAY
    MOV AL, DEFAULT_SEG
    OUT DX, AL

    MOV DX, PIC0
    MOV AL, ICW1
    OUT DX, AL

    MOV DX, PIC1
    MOV AL, ICW2
    OUT DX, AL

    MOV AL, ICW4
    OUT DX, AL

    MOV AL, OCW1
    OUT DX, AL

    XOR AX, AX
    MOV ES, AX

    MOV AX, OFFSET ISR0
    MOV [ES:VADD0], AX
    MOV AX, SEG ISR0
    MOV [ES:VADD0+2], AX

    MOV AX, OFFSET ISR1
    MOV [ES:VADD1], AX
    MOV AX, SEG ISR1
    MOV [ES:VADD1+2], AX

    STI

    XOR BX, BX
    MOV DX, LED_OUTPUT

MAIN_LOOP:
    MOV AL, BL
    OUT DX, AL

    MOV CX, BLINK_DELAY
DELAY_LOOP:
        NOP
        LOOP DELAY_LOOP

    NOT BX
    JMP MAIN_LOOP

    HLT

CODE    ENDS
        END START





